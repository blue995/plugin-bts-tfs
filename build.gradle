plugins {
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id 'java'
    id 'jacoco'
}

apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/5.0.0/build-quality.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/5.0.0/release-fat.gradle'

project.ext {
    releaseMode = project.hasProperty("releaseMode")
    pluginID = "tfs"
    pluginsDir = "$buildDir/plugins"
}

repositories {
    mavenCentral()
    releaseMode ? maven { url "http://dl.bintray.com/epam/reportportal" } : maven { url 'https://jitpack.io' }

}


dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:5.0.0' : 'com.github.reportportal:commons-bom:4b7ed8a')
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:plugin-api'
        annotationProcessor 'com.epam.reportportal:plugin-api'
        compile 'com.epam.reportportal:commons-model'

    } else {
        implementation 'com.github.reportportal:plugin-api:5.0.0-RC-5'
        annotationProcessor 'com.github.reportportal:plugin-api:5.0.0-RC-5'
        compile 'com.github.reportportal:commons-model:5.0.0-RC-5'
    }

    compile 'net.oauth.core:oauth:20090617'
    compile 'net.oauth.core:oauth-httpclient4:20090913'
    compile 'org.apache.tika:tika-core:1.14'
    implementation 'com.google.code.gson:gson'
}

wrapper {
    gradleVersion = '5.4.1'
}

jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
                "Plugin-Id": "${pluginID}",
                "Plugin-Version": "${project.version}",
                "Plugin-Provider": "Tobias Blaufuss",
                "Plugin-Class": "com.epam.reportportal.extension.bugtracking.tfs.TfsStrategyPlugin",
//                "Plugin-Dependencies": ""
        )
    }
}

shadowJar {
    zip64 true
    dependencies {
        include(dependency('com.google.guava:guava'))
        include(dependency('com.google.code.gson:gson'))
    }
}

task plugin(type: Jar) {
    baseName = "plugin-${pluginID}"
    into('classes') {
        with jar
    }
    into('lib') {
        from configurations.compile
    }
    extension('zip')
}

task assemblePlugin(type: Copy) {
    from plugin
    into pluginsDir
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}
